## Funciones y Fibras

Uno de los principios de los lenguajes de programación es evitar código repetitivo (_Don't Repeat Yourself_). Si una acción se debe ejecutar más de una vez se puede definir esa acción una única vez y luego reutilizarla cuantas veces se necesite.
Hay lenguajes de programación como https://elixir-lang.org/[Elixir] que son solamente funciones. Por lo que son una herramienta
muy poderosa.

Las funciones y fibras permiten reducir el trabajo necesario y su uso efectivo permite un código con menor cantidad de errores. 
En escencia son un conjunto de acciones que están agrupadas.

### https://wren.io/functions.html[Functions: Funciones]

En _Wren_ las funciones son objetos, esto significa que son iguales que cualquier otra variable y pueden ser usadas como
parámetros al igual que un _String_ o un número. Otros lenguajes como _Java_ no tienen esta característica.

En el sentido matemático podemos tener la siguiente expresión:

```txt
f(x) = (x + 10) / 2
```

Podemos interpretarla como

```txt
y = (x + 10) / 2
```

Lo cual puede ser expresado en _Wren_ de la siguiente forma:

```js
var y = Fn.new {|x| (x + 10) / 2}
```

Como se puede apreciar una función está definida por la clase `Fn`.
Se llama al método `new` para crear una nueva función.
El cuerpo de la función está definido por las llaves `{}`.
Los parámetros están definidos entre las barras `||`.

Para poder ejecutar la función y obtener su resultado, basta simplemente
con utilizar el método `call`.

```js
var y = Fn.new {|x| (x + 10) / 2}
var resultado = y.call(10) // x valdría 10
System.print(resultado) // muestra: 10
```

### https://wren.io/concurrency.html[Fibers: Fibras]

Las fibras son similares a las funciones, pero tienen la característica de que pueden ser
pausadas y re-ejecutadas a voluntad. Esto es especialmente útil para obtener concurrencia, 
es decir, ejecutar varias operaciones de forma paralela.

Todas las fibras en _Wren_ son manejadas por la máquina virtual de _Wren_. Lo cual es excelente
ya que no dependerá del sistema operativo para gatillar nuevos procesos, permitiendo ahorrar recursos.


En el siguiente ejemplo podemos ver como pausar y re-ejecutar una fibra.

```js
var fibra = Fiber.new {
  (1..4).each {|num|
    // Pausa la fibra y devuelve un valor
    Fiber.yield(num)
  }
}

System.print(fibra.call()) // muestra: 1

// Podemos ejecutar otras instrucciones y continuar la fibra
System.print(fibra.call()) // muestra: 2

// También podemos pasar un valor como parámetro adicional 
System.print(fibra.call(10)) // muestra: 3

System.print(fibra.call()) // muestra: 4
System.print(fibra.call()) // muestra: null
```

En una función esto no es posible ya que solo tendríamos acceso al último valor calculado.

```js
var funcion = Fn.new {
  var numero = null
  (1..4).each{|num|
    numero = num
  }
  return numero
}

System.print(funcion.call()) // muestra: 4
```

#### https://wren.io/error-handling.html[Manejo de Errores]

En muchos lenguajes se utiliza una secuencia llamada `try-catch`. En _Wren_ las fibras
son las encargadas de manejar los errores en tiempo de ejecución.

Utilizando el método `try` podemos ejecutar una fibra y capturar el error.

```js
var fibra = Fiber.new {
  123.noExiste
}

var error = fibra.try()
System.print("Hubo un Error: " + error)
```

Si deseamos generar un error propio podemos utilizar `Fiber.abort()`.

Este error puede ser un mensaje normal.

```js
Fiber.abort("Hubo un error")
```

O también un objeto personalizado.

```js
class MiError {
  construct nuevo() {}
  mensaje {"Este es un objeto error personalizado"}
}

var fibra = Fiber.new {
  Fiber.abort(MiError.nuevo())
}

fibra.try()

if (fibra.error is MiError) {
  System.print(fibra.error.mensaje)
}
```
